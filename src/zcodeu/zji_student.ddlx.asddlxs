@Metadata.layer: #CUSTOMER
@UI:{
    headerInfo:{ 
        typeName: 'Student',
        typeNamePlural: 'Students', 
        title:{ 
            type: #STANDARD, 
            label: 'Student', 
            value: 'StudentId' 
            } 
        }
    }
annotate view ZJI_STUDENT with
{

  @UI.facet: [
  { id : 'Student', 
    purpose: #STANDARD, 
    type: #IDENTIFICATION_REFERENCE, 
    label: 'Student' , 
    position: 10
  },
  { id : 'Academic' , 
    purpose: #STANDARD , 
    type: #LINEITEM_REFERENCE, 
    label: 'Academic Result',
    position:20, 
    targetElement: '_Academic' }
  ]
  @UI:{ lineItem: [{ position: 10  }]}
//  ,
//        identification: [{ position: 10  }]}
  StudentId;
  @UI:{ lineItem: [{ position: 20  }],
        selectionField: [{ position: 20  }],
        identification: [{ position: 20  }]}
  FirstName;
  @UI:{ lineItem: [{ position: 30  }],
        selectionField: [{ position: 30  }],
        identification: [{ position: 30  }]}
  LastName;
  @UI:{ lineItem: [{ position: 40  }],
        identification: [{ position: 40  }]}
  Age;
  @UI:{ lineItem: [{ position: 50  }],
        selectionField: [{ position: 50  }],
        identification: [{ position: 50  }]}
  @Consumption.valueHelpDefinition: [{ entity: { name: 'ZJI_COURSE' , element: 'Value' },
  distinctValues: true
  }]
  Course;
  @UI:{ lineItem: [{ position: 60  }],
        identification: [{ position: 60  }]}
  CourseDuration;
  @UI:{ lineItem: [{ position: 70  },
      { type: #FOR_ACTION, dataAction: 'setAdmitted', label: 'Set Admitted' }],
        identification: [{ position: 70  },
      { type: #FOR_ACTION, dataAction: 'setAdmitted', label: 'Set Admitted' }]}
  Status;
  @UI:{ lineItem: [{ position: 80  }],
        identification: [{ position: 80  }]}
  @Consumption.valueHelpDefinition: [{ entity: { name: 'ZJI_GENDER' , element: 'Value' },
  distinctValues: true,
  additionalBinding: [{ localElement: 'GenderDesc', element: 'Description' , usage: #FILTER_AND_RESULT}]
  }]
  Gender;
  @UI:{ identification: [ { position:85, label:'' } ] }
  GenderDesc;
  @UI:{ lineItem: [{ position: 90  }],
        identification: [{ position: 90  }]}
  Dob;
  @UI.hidden: true
  CreatedBy;
  @UI.hidden: true
  CreatedAt;
  @UI.hidden: true
  LastChangedBy;
  @UI.hidden: true
  LastChangedAt;
  @UI.hidden: true
  LocalLastChangedAt;
}